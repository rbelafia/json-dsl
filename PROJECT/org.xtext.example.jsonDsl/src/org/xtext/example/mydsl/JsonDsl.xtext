grammar org.xtext.example.mydsl.JsonDsl with org.eclipse.xtext.common.Terminals

generate jsonDsl "http://www.xtext.org/example/mydsl/JsonDsl"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Model:
	(stmts += SimpleStatement)*;


SimpleStatement:
	(Assignment | Expression | Print) ';'
;


Assignment:
	(leftHandSide=VariableCall) '=' (rightHandSide=Expression)
;

Expression: ExpressionLvl1;


ExpressionLvl1 returns Expression:
	ExpressionLvl2 ({DisjunctionExpression.left = current} 'OR' right = ExpressionLvl2)*
;

ExpressionLvl2 returns Expression:
	ExpressionLvl3 ({ConjunctionExpression.left = current} 'AND' right = ExpressionLvl3)*
;

ExpressionLvl3 returns Expression:
	ExpressionLvl4 (
		({EqualityExpression.left = current} '==' right = ExpressionLvl4)
		|({InequalityExpression.left = current} '!=' right = ExpressionLvl4)
		|({StrictEqualityExpression.left = current} '===' right = ExpressionLvl4)
		|({StrictInequalityExpression.left = current} '!==' right = ExpressionLvl4)
	)*
;


ExpressionLvl4 returns Expression:
	ExpressionLvl5 (
		({SuperiorExpression.left = current} '>' right = ExpressionLvl5)
		|({SuperiorOrEqualExpression.left = current} '>=' right = ExpressionLvl5)
		|({InferiorExpression.left = current} '<' right = ExpressionLvl5)
		|({InferiorOrEqualExpression.left = current} '<=' right = ExpressionLvl5)
	)*
;

ExpressionLvl5 returns Expression:
	ExpressionLvl6 (
		({AdditionExpression.left = current} '+' right = ExpressionLvl6)
		|({SubstractionExpression.left = current} '-' right = ExpressionLvl6)
	)*
;


ExpressionLvl6 returns Expression:
	ExpressionLvl7 (
		({MultiplicationExpression.left = current} '*' right = ExpressionLvl7)
		|({DivisionExpression.left = current} '/' right = ExpressionLvl7)
		|({ModuloExpression.left = current} '%' right = ExpressionLvl7)
	)*
;

ExpressionLvl7 returns Expression:
	UnaryMinusExpression | UnaryPlusExpression | ExpressionLvl8
;

UnaryMinusExpression:
	'-' sub=ExpressionLvl7
;

UnaryPlusExpression:
	'+' sub=ExpressionLvl7
;

ExpressionLvl8 returns Expression:
	LogicalNegationExpression | ExpressionLvl9
;

LogicalNegationExpression:
	'!' sub=ExpressionLvl8
;

ExpressionLvl9 returns Expression:
	PrimaryExpression (
		({ArrayCall.callee = current} '[' specifier = ArraySpecifier ']')
		|({FieldCall.callee = current} '~' field = STRING)
	)*
;

PrimaryExpression returns Expression:
	VariableCall | 
	Constant | 
	FunctionCall | 
	BracketExpression |
	PointerCall
;

BracketExpression:
	'(' sub=Expression ')'  
;

PointerCall:
	{PointerCall} '.'
;

VariableCall:
	name=ID
;

/* 
FieldCaller:
	callee = PrimaryExpression '~' key=STRING
;

ArrayCaller:
	callee = PrimaryExpression '[' specifier = ArraySpecifier ']'
;*/

ArraySpecifier:
	UnarySpecifier | RangeSpecifier
;

UnarySpecifier:
	index = INT
;

RangeSpecifier:
	from = INT ':' to = INT 
;



FunctionCall returns Expression:
	IOFunctions |
	InfoFunctions |
	ManipFunctions |
	ArithFunctions | 
	AlterFunctions
;

IOFunctions:
	Load | Store | Export
;

InfoFunctions :
	Depth | FieldInfo | Contains | Length
;

ManipFunctions :
	Select |
	Concat
;

ArithFunctions : Sum | Product ;

AlterFunctions :
	Delete
;


// IO FUNCTIONS

Load:
	'LOAD' fileName=STRING | 'LOAD' '(' fileName=STRING ')'
;


Store:
	'STORE' '(' expression=Expression ')' 'IN' '(' fileName=STRING ')' 
;

Export:
	'EXPORT_CSV' '(' expression=Expression ')' 'IN' '(' fileName=STRING ')'
;

Print:
	'PRINT' '(' expression=Expression ')'
;


// INFO FUNCTIONS

Depth:
	'GET_DEPTH' '(' expression=Expression ')'
;

FieldInfo:
	'GET_INFOS' '(' expression=Expression ')'
;

Contains:
	'IS' '(' keys+=Expression (',' keys+=Expression)*')'  'IN' '(' right=Expression ')'  
;


// MANIP

Select:
	'SELECT' '(' ('*' | (fields+=Expression (',' fields+=Expression)*)?) ')' 'FROM' '(' fromExpression=Expression ')' (=> 'WHERE' '(' whereExpression=Expression ')')? 
;

Concat:
	'CONCAT' '(' expressions+=Expression (',' expressions+=Expression) ')'
;

Length:
	'LENGTH' '(' expression=Expression ')'
;

Sum: 
	'SUM' '(' expression=Expression ')' ('ON' '(' field= Expression ')')? (=> 'WHERE' '(' whereExpression=Expression ')')?  
;

Product:
	'PRODUCT' '(' expression=Expression ')' ('ON' '(' field= Expression ')')? (=> 'WHERE' '(' whereExpression=Expression ')')?  
;

Delete:
	'DELETE' '(' ('*' | (fields+=Expression (',' fields+=Expression)*)?) ')' 'FROM' '(' fromExpression=Expression ')' (=> 'WHERE' '(' whereExpression=Expression ')')? 
;


Constant:
	Primitive | Array | JSonObject
;

Primitive:
	str=STRING | intNum=INT | floatNum=DOUBLE | bool=BOOL | nil=NULL
;

Array:
	{Array} '[' (values+=Expression (',' values+=Expression)*)? ']' 
;

JSonObject:
	{JSonObject} '{' (fields+=Field (',' fields+=Field)*)? '}'
;

Field:
	key=Expression ':' value=Expression
;

terminal NULL : 'null';

terminal BOOL : 'true' | 'false';

terminal DOUBLE : ('0'..'9')+'.'('0'..'9')*('e'('0'..'9')+)?;

